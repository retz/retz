buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        jcenter()
    }
    dependencies {
        classpath 'com.gradle:build-scan-plugin:1.8'
        classpath "com.github.jengelman.gradle.plugins:shadow:2.0.1"
        classpath 'com.netflix.nebula:gradle-ospackage-plugin:4.4.+'
        classpath "gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:0.14.0"
    }
}

def gitVersion() {
    def versionText = [
            "sh", "-c",
            "cd ${project.rootDir} ; git describe --always --tag"].execute().in.text.trim()
    versionText
}

apply plugin: 'com.gradle.build-scan'

allprojects {
    apply plugin: 'java'
    apply plugin: "com.github.hierynomus.license"

    repositories {
        mavenCentral()
        jcenter()
    }

    license {
        header rootProject.file('LICENSE-HEADER.template')
        strictCheck true
        ext.year = '2016-2017'
        ext.name = 'Nautilus Technologies, Inc.'

        excludes(["**/*.json", "**/*.jks"])
    }
}

subprojects {
    apply plugin: 'jacoco'
    apply plugin: 'scala'
    apply plugin: 'eclipse'
    apply plugin: 'findbugs'
    apply plugin: 'checkstyle'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'

    group = 'io.github.retz'
    // Note: You can't create RPM with hyphen included in version name
    version = '0.4.1-SNAPSHOT'
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    ext.retzMavenRepo = project.version.contains("-SNAPSHOT") ? "s3://retz-maven/snapshots" : "s3://retz-maven/releases"

    dependencies {
        compile 'org.slf4j:slf4j-api:1.7.25'

        testCompile 'junit:junit:4.12'
        testCompile 'org.hamcrest:hamcrest-library:1.3'

        testCompile 'org.scalacheck:scalacheck_2.12:1.13.+'
        testCompile 'org.scalatest:scalatest_2.12:3.0.+'
        testCompile 'org.scala-lang:scala-library:2.12.+'
    }

    jar {
        def versionText = gitVersion()
        manifest {
            attributes 'Git-Version': versionText
            attributes 'Artifact-Id': "${project.name}-${project.version}"
        }
    }

    task sourcesJar(type: Jar) {
        classifier 'sources'
        from project.sourceSets.main.allJava
    }
    artifacts {
        archives project.tasks.sourcesJar
    }

    eclipse {
        project.buildCommand 'org.eclipse.jdt.core.javabuilder'
    }

    test {
        useJUnit()
        testLogging { // Show that tests are run in the command-line output
            events 'failed', 'passed'
        }
    }

    jacocoTestReport {
        reports {
            html.enabled = true
            xml.enabled = true
            csv.enabled = false
            html.destination file("${buildDir}/reports/jacoco")
        }
    }

    checkstyle {
        toolVersion = "8.1"
        configFile = rootProject.file('checkstyle.xml')
        ignoreFailures = true
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact sourcesJar
            }
        }
        repositories {
            maven {
                url retzMavenRepo
                credentials(AwsCredentials) {
                    accessKey System.getenv('AWS_ACCESS_KEY_ID')
                    secretKey System.getenv('AWS_SECRET_ACCESS_KEY')
                }
            }
        }
    }

    task copyReport(type: Copy) {
        from "build/test-results"
          into "$rootDir/build/all-test-report"
    }
    test.finalizedBy(copyReport)
}

project(':retz-common') {
    dependencies {
        compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.+'
        compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8', version: '2.+'

        compile 'org.slf4j:slf4j-api:1.7.25'
        testCompile 'org.slf4j:slf4j-simple:1.7.25'
    }
}

def makePropertyFile(basedir, testType, name, version) {
    // A kludge workaround for clean build which don't have build/resources/main
    // where property file creation fails
    def subdir = new File(basedir, "/resources/${testType}")
    subdir.mkdirs();

    def serverName = "${name}-${version} (${gitVersion()})"

    // Bundle correctly corresponding file name; See RetzScheduler.setJarUri
    def prop = new Properties()
    prop.setProperty("servername", serverName)
    def propFile = new File(subdir, "retz-server.properties")
    propFile.createNewFile()
    propFile.withOutputStream { out ->
        prop.store(out, null)
    }
}

def makeClientPropertyFile(basedir, testType, name, version) {
    // A kludge workaround for clean build which don't have build/resources/main
    // where property file creation fails
    def subdir = new File(basedir, "/resources/${testType}")
    subdir.mkdirs();

    def fullVersion = "${name}-${version} (${gitVersion()})"

    def prop = new Properties()
    prop.setProperty("version", fullVersion)
    def propFile = new File(subdir, "retz-client.properties")
    propFile.createNewFile()
    propFile.withOutputStream { out ->
        prop.store(out, null)
    }
}


project(':retz-planner-spi') {

    dependencies {
        compile project(":retz-common")
        compile 'org.slf4j:slf4j-api:1.7.25'
    }
}
project(':retz-server') {
    apply plugin: 'application'
    apply plugin: "com.github.johnrengelman.shadow"
    apply plugin: 'nebula.ospackage'
    apply plugin: 'nebula.ospackage-application-daemon'

    dependencies {
        compile project(":retz-common")
        compile project(":retz-planner-spi")

        compile 'org.slf4j:slf4j-api:1.7.25'
        compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'

        compile 'commons-cli:commons-cli:1.4'
        compile 'commons-io:commons-io:2.5'

        compile group: 'com.j256.simplejmx', name: 'simplejmx', version: '1.15'

        compile 'org.apache.mesos:mesos:1.3.0'

        compile 'com.sparkjava:spark-core:2.6.0'
        compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.8.9'

        compile 'com.h2database:h2:1.4.+'
        compile group: 'org.postgresql', name: 'postgresql', version: '42.1.+'
        compile group: 'org.apache.tomcat', name: 'tomcat-jdbc', version: '8.5.+'

        testCompile project(":retz-client")
        testCompile project(":retz-admin")
    }

    task processResources.doLast {
        makePropertyFile("${buildDir}", "main", project.name, project.version)
    }

    task processTestResources.doLast {
        makePropertyFile("${buildDir}", "test", project.name, project.version)
    }

    run {
        if (project.hasProperty('args')) {
            args project.args.split('\\s+')
        }
    }

    mainClassName = 'io.github.retz.scheduler.Launcher'

    findbugs.excludeFilter = file("config/findbugs/excludeFilter.xml")

    ospackage {
        os = LINUX
        user = "root"
        summary = "A Job Scheduler on Mesos"
        license = "Apache 2.0"
        packager = 'Nautilus Technologies, Inc.'
        url = "https://retz.github.io/"
    }
    buildRpm {
        release = '1.el7'
        arch = 'X86_64'

        into '/opt/retz-server'
        from('src/main/dist/etc') {
            fileType CONFIG | NOREPLACE
            into 'etc'
        }
    }
    buildDeb {
        configurationFile('/opt/retz-server/etc/retz.properties')
        arch = 'AMD64'
    }

    configurations.archives.artifacts.removeAll { entry ->
        entry.type == 'zip' || entry.type == 'tar'
    }
}

project(':retz-client') {
    apply plugin: 'application'
    apply plugin: 'nebula.ospackage-application'
    apply plugin: "com.github.johnrengelman.shadow"

    dependencies {
        compile project(":retz-common")
        compile 'org.slf4j:slf4j-api:1.7.25'
        compile 'org.slf4j:slf4j-simple:1.7.25'

        compile "com.beust:jcommander:1.72"
        compile 'commons-io:commons-io:2.5'
        compile 'io.github.openfeign:feign-jackson:9.5.0'
        compile 'io.github.openfeign:feign-slf4j:9.5.0'
    }


    task processResources.doLast {
        makeClientPropertyFile("${buildDir}", "main", project.name, project.version)
    }

    task processTestResources.doLast {
        makeClientPropertyFile("${buildDir}", "test", project.name, project.version)
    }

    run {
        if (project.hasProperty('args')) {
            args project.args.split('\\s+')
        }
    }

    mainClassName = 'io.github.retz.cli.Launcher'
    // Won't set max heap size as large file download may happen
    applicationDefaultJvmArgs = ["-Xms8m", "-XX:+UseSerialGC"]

    ospackage {
        os = LINUX
        user = 'root' // https://github.com/nebula-plugins/gradle-ospackage-plugin/issues/13
    }
    buildRpm {
        release = '1.el7'
        arch = 'X86_64'

        into '/opt/retz-client'
        from('src/main/dist/etc') {
            fileType CONFIG | NOREPLACE
            into 'etc'
        }
    }
    buildDeb {
        arch = 'AMD64'
        configurationFile('/opt/retz-client/etc/retz.properties')
    }

    configurations.archives.artifacts.removeAll { entry ->
        entry.type == 'zip' || entry.type == 'tar'
    }
}

project('retz-admin') {
    apply plugin: 'java'
    apply plugin: 'application'
    apply plugin: 'nebula.ospackage-application'
    apply plugin: "com.github.johnrengelman.shadow"

    dependencies {
        compile project(":retz-common")

        testCompile group: 'junit', name: 'junit', version: '4.12'

        compile 'org.slf4j:slf4j-api:1.7.25'
        compile 'org.slf4j:slf4j-simple:1.7.25'
        compile group: 'com.j256.simplejmx', name: 'simplejmx', version: '1.15'
        compile "com.beust:jcommander:1.72"
    }

    mainClassName = 'io.github.retz.admin.Launcher'
    // Won't set max heap size as large file download may happen
    applicationDefaultJvmArgs = ["-Xms8m", "-XX:+UseSerialGC"]

    ospackage {
        os = LINUX
        user = 'root' // https://github.com/nebula-plugins/gradle-ospackage-plugin/issues/13
    }
    buildRpm {
        release = '1.el7'
        arch = 'X86_64'
    }
    buildDeb {
        arch = 'AMD64'
    }

    configurations.archives.artifacts.removeAll { entry ->
        entry.type == 'zip' || entry.type == 'tar'
    }
}
project('retz-inttest') {
    dependencies {
        testCompile project(":retz-client")
        testCompile 'com.github.docker-java:docker-java:3.0.10'
        testCompile 'io.github.openfeign:feign-jackson:9.5.0'
        testCompile 'io.github.openfeign:feign-slf4j:9.5.0'
    }

    task copyServerJar(type: Copy, dependsOn: ':retz-server:shadowJar') {
        from project(":retz-server").shadowJar.archivePath
        into "${buildDir}/libs/"
        rename(/(retz-server)-.+-(all)/, '$1-$2')
    }
    task copyClientJar(type: Copy, dependsOn: ':retz-client:shadowJar') {
        from project(":retz-client").shadowJar.archivePath
        into "${buildDir}/libs/"
        rename(/(retz-client)-.+-(all)/, '$1-$2')
    }
    task copyAdminJar(type: Copy, dependsOn: ':retz-admin:shadowJar') {
        from project(":retz-admin").shadowJar.archivePath
        into "${buildDir}/libs/"
        rename(/(retz-admin)-.+-(all)/, '$1-$2')
    }
    task copy(dependsOn: ['copyServerJar', 'copyClientJar', 'copyAdminJar'])

    // Retz- clients classes brought to build/classes, while bundled resources are not
    task processTestResources.doLast {
        makeClientPropertyFile("${buildDir}", "test", project.name, project.version)
    }

    task buildDocker {
        doLast {

            // Use latest Mesos by default
            def mesos_version = System.getProperty('mesos_version', '')
            System.err.println("mesos version = ${mesos_version}")

            def mesos_package_name = 'mesos'
            if (mesos_version != null && ! mesos_version.isEmpty() ) {
                // 1.1.1-2.0.1 and 1.0.3-2.0.1 are not supported
                // run `yum --showduplicates list mesos` to know all available packages at Mesosphere repository
                def supported_mesos_versions = ['1.2.1-2.0.1', '1.2.2-2.0.1',
                                                '1.3.0-2.0.3', '1.3.1-2.0.1',
                                                '1.4.0-2.0.1']
                if (! supported_mesos_versions.contains(mesos_version)) {
                    System.err.println("Mesos version should be one of " + supported_mesos_versions + " but is: " + mesos_version)
                    exit
                }
                mesos_package_name = "mesos-${mesos_version}"
            }
            System.err.println("mesos package name = ${mesos_package_name}")

            exec {
                executable "docker"
                args "build", "-t", "mesos-retz", "--build-arg",
                        "MESOS_PACKAGE_NAME=${mesos_package_name}",
                        "${projectDir}/src/test/resources/docker"
            }
        }
    }

    test {
        // Execute test suites only if "inttest" property is explicitly passed,
        // like "gradlew test -Dinttest"
        ext.integration = System.getProperty('inttest', 'false') != 'false'
        inputs.property 'integration', integration
        if (integration) {
            dependsOn buildDocker, copy
        }
        onlyIf { integration }
    }

    task cleanDockerIamges {
        doLast {
            exec {
                executable "sh"
                args "-c", "docker images -q -f dangling=true | xargs docker rmi"
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.1'
    distributionType = 'ALL'
}

buildScan {
    licenseAgreementUrl = 'https://gradle.com/terms-of-service'
    licenseAgree = 'yes'
    link 'GitHub', 'https://github.com/retz/retz'
}
